apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

// https://gist.github.com/JonasGroeger/7620911
def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12
    /*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
    // def isRef = head.length > 1     // ref: refs/heads/master

    if(isCommit) return head[0].trim().take(takeFromHash) // e5a7c79edabb

    def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
    refHead.text.trim().take takeFromHash
}

mainClassName = 'me.rockybreslow.redordead.Main'
version = getCheckedOutGitCommitHash() + '-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Processing
    compile 'org.processing:core:3.0.1'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }

    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
}

jar {
    manifest {
        attributes('Main-Class': mainClassName)
    }

    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
